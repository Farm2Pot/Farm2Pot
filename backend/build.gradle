plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

allprojects {
    group = 'farm2pot'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = rootProject.group
    version = rootProject.version

    // Java toolchain
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.3"
        }
    }

    dependencies {
        // 공통 의존성
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.mysql:mysql-connector-j'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        compileOnly 'org.projectlombok:lombok:1.18.28'
        annotationProcessor 'org.projectlombok:lombok:1.18.28'
    }

    // 공통 컴파일 설정
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.release.set(21)
    }
}

// core 모듈
project(":core") {
    apply plugin: 'org.springframework.boot'
}

// user 모듈
project(":user") {
    apply plugin: 'org.springframework.boot'
    dependencies {
        implementation project(':core')
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
}

// admin 모듈
project(":admin") {
    apply plugin: 'org.springframework.boot'
    dependencies {
        implementation project(':core')
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
}

// subscription 모듈
project(":subscription") {
    apply plugin: 'org.springframework.boot'
    dependencies {
        implementation project(':core')
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
}
